# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=vim-buftabline-git
_pkgname=vim-buftabline
pkgver=r74.73b9ef5
pkgrel=1
arch=('any')
url='https://github.com/ap/vim-buftabline'
license=('MIT')
makedepends=('git')
pkgdesc="Buffer list that lives in the tabline"
depends=('vim')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find doc plugin -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/vim/vimfiles/{}" \;
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
