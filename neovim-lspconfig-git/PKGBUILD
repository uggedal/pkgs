# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-lspconfig-git
_pkgname=neovim-lspconfig
pkgver=r1010.e808cea
pkgrel=1
arch=('any')
url='https://github.com/neovim/nvim-lspconfig'
license=('Apache')
makedepends=('git')
pkgdesc="Quickstart configurations for the Nvim LSP client"
depends=('neovim')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find autoload doc lua plugin -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
	install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
