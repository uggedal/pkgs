# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-nightbuddy-git
_pkgname=neovim-nightbuddy
pkgver=r5.ee466f7
pkgrel=1
arch=('any')
url='https://github.com/DilanGMB/nightbuddy'
license=('MIT')
makedepends=('git')
pkgdesc="Minimalist dark colorschemes for neovim"
depends=('neovim' 'neovim-colorbuddy-git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find colors lua -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
