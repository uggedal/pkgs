# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=vim-picker-git
_pkgname=vim-picker
pkgver=r132.06c8ee4
pkgrel=1
arch=('any')
url='https://github.com/srstevenson/vim-picker'
license=('custom')
makedepends=('git')
pkgdesc="Fuzzy picker for Neovim and Vim"
depends=('vim')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find autoload doc ftplugin plugin -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/vim/vimfiles/{}" \;
	install -Dm 644 LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
