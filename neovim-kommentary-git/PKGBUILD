# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-kommentary-git
_pkgname=neovim-kommentary
pkgver=r64.f0b6d75
pkgrel=1
arch=('any')
url='https://github.com/b3nj5m1n/kommentary'
license=('GPL')
makedepends=('git')
pkgdesc="Neovim commenting plugin"
depends=('neovim')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find lua plugin -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
