# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-bullets-git
_pkgname=neovim-bullets
pkgver=r208.486f97d
pkgrel=1
arch=('any')
url='https://github.com/dkarter/bullets.vim'
license=('GPL')
makedepends=('git')
pkgdesc="Neovim plugin for automated bullet lists"
depends=('neovim')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find doc plugin -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
	install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
