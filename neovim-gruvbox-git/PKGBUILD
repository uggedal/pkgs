# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-gruvbox-git
_pkgname=neovim-gruvbox
pkgver=r68.d789a80
pkgrel=1
arch=('any')
url='https://github.com/npxbr/gruvbox.nvim'
license=('MIT')
makedepends=('git')
pkgdesc="Port of gruvbox community theme to lua with treesitter support"
depends=('neovim' 'neovim-lush-git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find colors lua -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
