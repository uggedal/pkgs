# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-onebuddy-git
_pkgname=neovim-onebuddy
pkgver=r22.7e16006
pkgrel=1
arch=('any')
url='https://github.com/Th3Whit3Wolf/onebuddy'
license=('MIT')
makedepends=('git')
pkgdesc="Light and dark atom one theme for neovim"
depends=('neovim' 'neovim-colorbuddy-git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find colors lua -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
