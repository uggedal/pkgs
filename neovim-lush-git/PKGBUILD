# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-lush-git
_pkgname=neovim-lush
pkgver=r227.3db2152
pkgrel=1
arch=('any')
url='https://github.com/rktjmp/lush.nvim'
license=('MIT')
makedepends=('git')
pkgdesc="Define Neovim themes as a DSL in lua, with real-time feedback"
depends=('neovim')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find doc lua plugin -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
