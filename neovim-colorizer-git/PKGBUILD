# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-colorizer-git
_pkgname=neovim-colorizer
pkgver=r31.36c610a
pkgrel=1
arch=('any')
url='https://github.com/norcalli/nvim-colorizer.lua'
license=('GPL')
makedepends=('git')
pkgdesc="The fastest Neovim colorizer"
depends=('neovim')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find doc lua plugin -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
