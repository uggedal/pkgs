# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-compe-git
_pkgname=neovim-compe
pkgver=r373.efe3a66
pkgrel=1
arch=('any')
url='https://github.com/hrsh7th/nvim-compe'
license=('MIT')
makedepends=('git')
pkgdesc="Auto completion plugin for nvim"
depends=('neovim')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find after autoload doc lua plugin -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
