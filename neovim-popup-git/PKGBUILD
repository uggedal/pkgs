# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-popup-git
_pkgname=neovim-popup
pkgver=r15.5e3bece
pkgrel=1
arch=('any')
url='https://github.com/nvim-lua/popup.nvim'
license=('custom')
makedepends=('git')
pkgdesc="Implementation of vim popup API in Neovim"
depends=('neovim')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find lua -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
}
