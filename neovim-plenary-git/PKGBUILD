# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-plenary-git
_pkgname=neovim-plenary
pkgver=r148.d4476cd
pkgrel=1
arch=('any')
url='https://github.com/nvim-lua/plenary.nvim'
license=('MIT')
makedepends=('git')
pkgdesc="Collection of lua functions for Neovim"
depends=('neovim')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find data lua plugin -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
