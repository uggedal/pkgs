# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-treesitter-git
_pkgname=neovim-treesitter
pkgver=r1296.2873d1e
pkgrel=1
arch=('any')
url='https://github.com/nvim-treesitter/nvim-treesitter'
license=('Apache')
makedepends=('git' 'nodejs')
pkgdesc="Neovim Treesitter configurations and abstraction layer "
depends=('neovim-git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')
_languages=(
	bash
	c
	c_sharp
	comment
	cpp
	css
	go
	html
	java
	javascript
	json
	kotlin
	latex
	lua
	python
	regex
	swift
	toml
	typescript
	yaml
)

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

_install() {
	find after autoload doc ftdetect lua plugin queries -type f -exec \
		install -Dm 644 '{}' "${1}/{}" \;
}

build() {
	cd "${srcdir}/${_pkgname}"
	_install "$HOME/.config/nvim"

	local l
	for l in "${_languages[@]}"; do
		nvim --headless -c "TSInstallSync ${l}" -c qa
	done
}

package() {
	cd "${srcdir}/${_pkgname}"
	_install "${pkgdir}/usr/share/nvim/runtime"

	cd "$HOME/.local/share/nvim/site"
	find parser -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
}
