# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-telescope-git
_pkgname=neovim-telescope
pkgver=r556.ad30a7b
pkgrel=1
arch=('any')
url='https://github.com/nvim-telescope/telescope.nvim'
license=('MIT')
makedepends=('git')
pkgdesc="Collection of lua functions for Neovim"
depends=('neovim' 'neovim-popup-git' 'neovim-plenary-git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find data doc lua plugin -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
