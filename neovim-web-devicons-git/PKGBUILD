# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=neovim-web-devicons-git
_pkgname=neovim-web-devicons
pkgver=r56.1db2738
pkgrel=1
arch=('any')
url='https://github.com/kyazdani42/nvim-web-devicons'
license=('custom')
makedepends=('git')
pkgdesc="Adds file type icons to Neovim plugins"
depends=('neovim')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find lua plugin -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/nvim/runtime/{}" \;
}
