# Maintainer: Eivind Uggedal <eivind@uggedal.com>
pkgname=vim-taskwiki-git
_pkgname=taskwiki
pkgver=r845.23a3c20
pkgrel=2
arch=('any')
url='https://github.com/tools-life/taskwiki'
license=('custom')
makedepends=('git')
pkgdesc="Proper project management with Taskwarrior in vim"
depends=('vim' 'python-tasklib' 'python-six' 'task' 'vim-vimwiki')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/${_pkgname}"
	find after autoload doc ftplugin taskwiki -type f -exec \
		install -Dm 644 '{}' "${pkgdir}/usr/share/vim/vimfiles/{}" \;
	install -Dm 644 LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
